def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

buildscript {
    rootProject.ext.adyenReactNativeRedirectScheme = "adyenreactnative"
    apply from: './dependencies.gradle'

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdk safeExtGet('compileSdkVersion', 34)
    defaultConfig {
        minSdkVersion safeExtGet('minSdkVersion', 21)
        targetSdkVersion safeExtGet('targetSdkVersion', 34)
        versionCode 1
        versionName "1.0"
        buildConfigField "String", "adyenReactNativeRedirectScheme", "\"$rootProject.ext.adyenReactNativeRedirectScheme\""
        buildConfigField "String", "CHECKOUT_VERSION", "\"{SDK_VERSION}\""
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }
    lintOptions {
        disable 'GradleCompatible'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    mavenLocal()
    maven {
        // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
        url("$rootDir/../node_modules/react-native/android")
    }
    google()
    mavenCentral()
    jcenter()
}

dependencies {
    //noinspection GradleDynamicVersion
    api "com.facebook.react:react-native:+"  // From node_modules
    implementation "com.adyen.checkout:drop-in:$adyen_version"
    implementation "com.google.android.material:material:$material_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"

    //Tests
    testImplementation testLibraries.json
    testImplementation testLibraries.junit5
    testImplementation testLibraries.kotlinCoroutines
    testImplementation testLibraries.mockito
}
